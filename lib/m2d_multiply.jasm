; ======================================================
; multiply two 2x2 matrices
; 
; Performs a multiplication of two 2D matrixes. This 
; function uses the memory map defined in the "def_m2d" 
; file. The result can fetched by loading from the 
; specified memory location.
; 
; |A1 B1|   |A2 B2|   |A3 B3|
; |C1 D1| * |C2 D2| = |C3 D3|
; 
; A3 = (A1, B1) dot (A2, C2)
; B3 = (A1, B1) dot (B2, D2)
; C3 = (C1, D1) dot (A2, C2)
; D3 = (C1, D1) dot (B2, D2)
; 
; labels:
; m2d_multiply
; _m2d_multiply_sub
; 
; definitions:
; M1_A
; M1_B
; M1_C
; M1_D
; 
; M2_A
; M2_B
; M2_C
; M2_D
; 
; M3_A
; M3_B
; M3_C
; M3_D
; 
; using:
; m2d_defines.jasm
; m_multiply.jasm
; 
; author: 
; Michiel Drost - Nullpointer Works
; 
; updated: 
; 05-03-2021
; 
; ======================================================

.inc "m2d_defines.jasm"
.inc "m_multiply.jasm"

m2d_multiply:
	push a
	push b
	push c
	push d
	push r0
	
	; calc A3
	load a,M1_A
	load b,M2_A
	load c,M1_B
	load d,M2_C
	call _m2d_multiply_sub
	load M3_A,r0
	
	; calc B3
	load a,M1_A
	load b,M2_B
	load c,M1_B
	load d,M2_D
	call _m2d_multiply_sub
	load M3_B,r0
	
	; calc C3
	load a,M1_C
	load b,M2_A
	load c,M1_D
	load d,M2_C
	call _m2d_multiply_sub
	load M3_C,r0
	
	; calc D3
	load a,M1_C
	load b,M2_B
	load c,M1_D
	load d,M2_D
	call _m2d_multiply_sub
	load M3_D,r0
	
	pop r0
	pop d
	pop c
	pop b
	pop a
	ret
	
_m2d_multiply_sub:
	call m_multiply
	load r0,a
	load a,c
	load b,d
	call m_multiply
	add r0,a
	ret
